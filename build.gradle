plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.jreleaser' version '1.13.1'
}

group = 'fr.skogrine'
version = '1.0.0'
description = "UtilityCM offers powerful, reusable classes for modern Java development."

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    implementation('redis.clients:jedis:5.1.3')
    implementation 'org.javassist:javassist:3.28.0-GA'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

ext {
    githubToken = project.findProperty('githubToken') ?: ''
}

jreleaser {
    project {
        authors = ['Skogrine']
        license = 'MIT'
        links {
            homepage = "https://github.com/Skogrine/UtilityCM"
        }
        inceptionYear = '2024'
    }

    release {
        github {
            repoOwner = 'Skogrine'
            overwrite = true
            token = githubToken
        }
    }

    distributions {
        app {
            artifact {
                path = 'build/distributions/{{distributionName}}-{{projectVersion}}.zip'
            }
        }
    }
}


tasks.register("showMeCache") {
    doLast {
        configurations.compileClasspath.each { println it }
    }
}

test {
    useJUnitPlatform()

    jvmArgs = [
            '--add-opens', 'java.base/java.lang=ALL-UNNAMED',
            '--add-opens', 'java.base/java.lang.reflect=ALL-UNNAMED'
    ]
}
